buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("io.swagger:swagger-codegen:2.3.1")
        classpath "io.redskap:swagger-brake-gradle:1.0.0"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.6.0"
    }
}

group 'io.redskap.example'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: "com.jfrog.artifactory"
apply plugin: "io.redskap.swagger-brake"

version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}
import io.swagger.codegen.DefaultGenerator
import io.swagger.codegen.config.CodegenConfigurator

ext.artifactoryBaseUrl  = "http://localhost:8082"
ext.artifactoryUsername = "admin"
ext.artifactoryPassword = "password"

swaggerBrake {
    mavenRepoUrl = "${artifactoryBaseUrl}/artifactory/libs-snapshot-local"
    mavenRepoUsername = artifactoryUsername
    mavenRepoPassword = artifactoryPassword
    newApi = "${project.buildDir}/resources/main/swagger.yaml"
}

artifactory {
    contextUrl = "${artifactoryBaseUrl}/artifactory"
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = artifactoryUsername
            password = artifactoryPassword
        }
        defaults {
            publications ('mavenJava')
            publishConfigs('archives', 'published')
            publishBuildInfo = true
            publishArtifacts = true
            publishPom = true
        }
    }
    clientConfig.info.setBuildNumber('' + new java.util.Random(System.currentTimeMillis()).nextInt(20000))
    clientConfig.timeout = 600
}

task generateApi {
    doLast {
        def config = new CodegenConfigurator()
        config.setLang("swagger")
        config.setInputSpec("${project.rootDir}/src/main/resources/swagger.yaml")
        config.setOutputDir(project.buildDir.toString())
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

compileJava.dependsOn('generateApi')